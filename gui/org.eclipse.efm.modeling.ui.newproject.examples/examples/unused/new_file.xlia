@xlia< system , 1.0 >:

system mySystem {
@public:
	type myEnum enum { OFF , ON }

	port input myInputPort( myEnum );
	port output myOutputPort( int );

@behavior:
	statemachine myStatemachine "my state machine" {
	@private:
		var integer n;
		var myEnum trigger = OFF;

	@region:
		state< start > q0 "my start state" {
			@enable{
				n = 0;
			}
			transition t0 "my simple transition" {
				input myInputPort( trigger );
			} --> qChoice;
		}

		state< choice > qChoice "my choice pseudostate" {
			transition t1 {
				guard( trigger == ON );
				output myOutputPort( 42 );
			} --> q1;

			transition< else > t2 "my else transition" --> q0;
		}

		state q1 "my simple state" {
			transition t3 {
				input myInputPort( trigger );
				guard( trigger == OFF );
				n = 1;
			} --> qFinal;

			transition< else > t4 "my else transition" --> q0;
		}

		state< final > qFinal "my final state";
	}

@com:
	connect< env > {
		input myInputPort;
		output myOutputPort;
	}
}
